#!html

<div style="text-align: center"><img width=150px src="https://fsharp.org/img/logo/fsharp256.png"></div>

#!markdown

# Introduction to F# #

F# is an open-source and cross-platform language that's supported by the [F# Software Foundation](http://fsharp.org) and a worldwide community of contributors. Microsoft and other companies develop professional tooling for F#.

As a language, F# excels at succinct, correct, and maintainable code, and is scalable from small scripting projects to enterprise-level software. F# is used for [data scripting](https://fsharpforfunandprofit.com/posts/low-risk-ways-to-use-fsharp-at-work-2/), [data science](http://fslab.org), [web programming](http://fsharp.org/guides/web/), and component development. It interoperates with a wide range of software libraries and tools, and you can use all .NET and C# libraries with F#.

F# is well suited for Jupyter notebooks because declarations are ordered in a script-like way. This present notebook, which provides introductory F# example, assumes you are familiar with basic programming structures already. There are also many other [F# learning resources](http://fsharp.org/learn.html), including the[F# Cheatsheet](https://dungpa.github.io/fsharp-cheatsheet/) is a quick guide. 

## F# Basics #

F# 基础语法和算法

#!fsharp

(12/4 + 5 + 7) * 4 - 18

#!markdown

自动推演符号

#!fsharp

let sampleNumbers = [ 0 .. 15 ]

sampleNumbers

#!markdown

函数声明（带类型）

#!fsharp

/// A function to compute a sample curve
let sampleFunction (x:int) = 
    2*x*x - 5*x + 3

sampleFunction 7

#!fsharp

sampleFunction (7 + 4)

#!markdown

Note that F#, like Python, uses indentation to define blocks. You can find out more about this in the topic [F# syntax: indentation and verbosity](https://fsharpforfunandprofit.com/posts/fsharp-syntax/).

显式类型转换

#!fsharp

sampleFunction (int 3.14 + int 2.79)

#!markdown

条件表达式（行内）和bu

#!fsharp

if 98.0 < 100.0 then 10 else 20

#!fsharp

not false && (true || false)

#!markdown

### Strings, Tuples, Lists and Arrays

String literals use quotes or triple-quotes, and you can concatenate strings with the `+` operator:

#!fsharp

let helloWorld = "Hello" + " " + "world"

helloWorld

#!fsharp

let tripleQuoteString = """A triple-quoted string can contain double quotes " anywhere within it."""

tripleQuoteString

#!markdown

A tuple combines multiple data items into one value. Below is a tuple consisting of an integer, a string, and a double-precision floating point number:

#!fsharp

(1, "fred", 3.1415)

#!markdown

Lists are linear sequences of values of the same type. The code below generates a list containing all the Fridays of the first half of 2017. This example also shows you how to use some of the very extensive .NET libraries, all of which are available to F#. Find out more about the .NET libraries in online resources such as the [.NET Framework API Reference](https://docs.microsoft.com/en-us/dotnet/).

#!fsharp

open System

let fridaysList = 
    [ for month in 1 .. 6 do
        for day in 1 .. DateTime.DaysInMonth(2017, month) do 
          let date = DateTime(2017, month, day)
          if date.DayOfWeek = DayOfWeek.Friday then 
            yield date.ToShortDateString() ]
              
fridaysList              

#!markdown

Arrays are similar to lists but are mutable and are stored as flat data rather than linked lists:

#!fsharp

let lowNumbers = [| 1 .. 200 |]

lowNumbers

#!markdown

Lists, arrays and sequences can be processed using functions. Use the pipeline operator `|>` and a function to process data using `List.map`:

#!fsharp

sampleNumbers 
|> List.map (fun x -> x*x) 

#!markdown

Pipelines can be chained together.  The following pipeline computes the sum of a selection of square numbers:

#!fsharp

let sumOfSelectedSquares = 
    sampleNumbers
    |> List.map (fun x -> x*x) 
    |> List.filter (fun x -> x % 3 = 0)
    |> List.sumBy (fun x -> x * x)
    
sumOfSelectedSquares    

#!markdown

Both lists and arrays can use slicing notation (note that unlike Python, F# slicing notation is inclusive of the upper bound):

#!fsharp

lowNumbers.[0 .. 50]

#!markdown

### Sequences

Sequences are computed on-demand and are specified using the same constructs as lists and arrays.  Here is an infinite sequence which is a random walk. Use `yield!` to return each element of a subsequence:

#!fsharp

let rnd = System.Random()

let rec randomWalk x =
    seq { yield x
          yield! randomWalk (x + rnd.NextDouble() - 0.5) }

let firstValuesOfRandomWalk = 
    randomWalk 5.0 
    |> Seq.truncate 20
    |> Seq.toList
    
firstValuesOfRandomWalk

#!markdown

### Type Definitions

F# is a typed language. The example below defines a record type. Learn more about F# type definitions in online resources such as [F# for Fun and Profit](https://fsharpforfunandprofit.com/posts/overview-of-types-in-fsharp/).

The type definition uses an option value. Option values are any kind of value tagged with either `Some` or `None`. They're used extensively in F# code to represent the cases where many other languages would use null references.

#!fsharp

type ContactCard = 
    { Name     : string
      Phone    : string
      Verified : bool
      ZipCode : string option}            
      
let sampleCard = { Name = "Alf" ; Phone = "(206) 555-0157" ; Verified = false; ZipCode=Some "90210" }

sampleCard

#!fsharp

let showCard (c: ContactCard) = 
    c.Name + " Phone: " + c.Phone + (if not c.Verified then " (unverified)" else "")
    
showCard sampleCard

#!markdown

### UnitsOfMeasure

Code can be annotated with units of measure when using F# arithmetic over numeric types:

#!fsharp

open Microsoft.FSharp.Data.UnitSystems.SI.UnitNames

let raceLength = 1600.0<meter>

[<Measure>]
type mile =
    /// Conversion factor mile to meter: meter is defined in SI.UnitNames
    static member asMeter = 1609.344<meter/mile>

/// Distance expressed using imperial units
let distanceToWalk  = 500.0<mile>          

// Same distanceusing metric system
let distanceToWalkInMeters = distanceToWalk * mile.asMeter   

(raceLength, distanceToWalk, distanceToWalkInMeters)

#!markdown

### 并行编程

你可以使用`Array.Parallel.map`和`Async.Parallel`或者相关函数实现并行处理collections

#!fsharp

let oneBigArray = [| 0 .. 100000 |]

// Do some CPU intensive computation
let rec computeSomeFunction x = 
    if x <= 2 then 1 
    else computeSomeFunction (x - 1) + computeSomeFunction (x - 2)
       
/// Do a parallel map over a large input array
let computeResults() = oneBigArray |> Array.Parallel.map (fun x -> computeSomeFunction (x % 24))

computeResults()

#!markdown

### Learning More

A range of F# learning topics are available online at [The F# Software Foundation](http://fsharp.org/learn) and also the [Microsoft .NET Docs F# Guide](https://docs.microsoft.com/dotnet/fsharp/).

#!markdown

# Special Features of F# in Azure Notebooks

Azure Notebooks uses [`IfSharp`](http://fsprojects.github.io/IfSharp/) to provide F# Notebooks. This section will take you through some of the nice ways you can combine F# features with the display mechanisms provides by `IfSharp`.

#!markdown

## Referencing NuGet Packages using Paket

There are thousands of well-engineered packages available for use with F#.  Most are provided via [nuget.org](http://nuget.org). You can use the [Paket dependency manager](https://fsprojects.github.io/Paket/) to add references to NuGet packages to your F# Azure Notebook.

#!fsharp

#load "Paket.fsx"

#!markdown

The following code installs the selected packages; it may take a minute to run on first use with a particular kernel.

#!fsharp

Paket.Package
  [ "MathNet.Numerics"
    "MathNet.Numerics.FSharp"
  ]

#!markdown

You can now use these packages from your code using this reference:

#!fsharp

#load "Paket.Generated.Refs.fsx"

#!markdown

For example, you can use the MathNet.Numerics linear algebra libraries:

#!fsharp

open MathNet.Numerics.LinearAlgebra
let m = matrix [[ 1.0; 2.0 ]
                [ 3.0; 4.0 ]]
m.Inverse() 

#!markdown

## Tables

Let's start with using `Util.Table` to display a nicely formatted table:

#!fsharp

type MyType = { FirstName: string; LastName: string }
let records = 
    [|
        { FirstName = "Walter"; LastName = "Harp" }
        { FirstName = "Jeff"; LastName = "Smith" }
        { FirstName = "Ben"; LastName = "Smith" }
        { FirstName = ""; LastName = "Holly" }
    |]
records

#!markdown

## Formatting text using LaTeX

The following example composes some LaTeX using `LatexOutput` and the `Util.Math` function provided by `IfSharp`:

#!fsharp

@"H← 60 + \frac{30(B−R)}{Vmax−Vmin}  , if Vmax = G"

#!markdown

Lovely! You've used `Util.Math: string -> LatexOutput` to format LaTeX. The result of your code snippet is implictly passed to the `Display` function and shown in your output. Next we format some operators:

#!fsharp

"\int_0^\infty e^{-x^2} dx \mathrm{\ in\ \LaTeX{}}"

#!markdown

The result of your code snippet is implictly passed to the `Display` function and shown in your output. The `Display` function is provided by `IfSharp`. It takes any object and attempts to display it in your notebook. By default, `Display` already handles many types (including `LatexOutput`), and later we'll see how to extend it to handle any type you need.

You can also call `Display` in your code, in order to display more than one output.

#!markdown

## Charts

We can display charts, using [`XPlot.Plotly`](https://tahahachana.github.io/XPlot/plotly.html):

#!fsharp

// #load "XPlot.Plotly.Paket.fsx"
// #load "XPlot.Plotly.fsx"
open XPlot.Plotly

#!markdown

For example:

#!fsharp

Heatmap(z = [[1; 20; 30]; [20; 1; 60]; [30; 60; 1]])
    |> Chart.Plot
    |> Chart.WithLayout (Layout(title = "Simple Heatmap"))

#!fsharp

["giraffes", 20; "orangutans", 14; "monkeys", 23]
    |> Chart.Bar
    |> Chart.WithLayout (Layout(title = "Basic Bar Chart"))
    |> Chart.WithHeight 300
    |> Chart.WithWidth 400

#!markdown

Note that we had to `#load` two helper scripts in order to load the assemblies we need and to enable `Display` to show our charts. The first downloads and installs the required Paket packages, and the second sets up `Display` support.

#!markdown

## Computed inline HTML 

You can also format HTML and display it by producing values of a special type ``HtmlOutput`` (a record type with one field ``Html``):

#!fsharp

 HtmlString """<div style="background-color: #007fff; font-weight: bold;">Would you like some toast?</div>"""

// Exercise - try video content:
//  <video width="480" controls poster="https://archive.org/download/WebmVp8Vorbis/webmvp8.gif" >
//    <source src="https://archive.org/download/WebmVp8Vorbis/webmvp8.webm" type="video/webm">
//    <source src="https://archive.org/download/WebmVp8Vorbis/webmvp8_512kb.mp4" type="video/mp4">
//    <source src="https://archive.org/download/WebmVp8Vorbis/webmvp8.ogv" type="video/ogg">
//    Your browser doesn't support HTML5 video tag.
//  </video>"""

#!markdown

This type renders its content directly as HTML.

#!markdown

Certain sites of content are whitelisted for use from Azure Notebooks.  At the time of writing these include `raw.githubusercontent.com`, `github.com`, `onedrive.live.com` and a range of Azure services. You can include content from these sites directly through `Util.Url`:

#!fsharp

"https://github.com/fsharp/fsfoundation/raw/gh-pages/img/logo/fsharp256.png"
  |> display

#!markdown

> **Note**: Evaluation uses a REPL
>
> Notice that we used a value from an earlier snippet in a later one. Your entire notebook is a single program running in an `fsi` REPL, so you accumulate data and functions as you go. The caveat is that code cells aren't reevaluated unless you specifically run them. If you go back and edit an earlier snippet, then, you can reference objects that are defined in later code cells! If you reevaluate your entire notebook, this causes errors.
>
> Just remember that your notebook is a single REPL instance, and you won't be surprised by this behavior.

#!markdown

## Custom `Display` Printers

The `Display` function can show many things in your F# Azure Notebook already, but you can extend it to display any type in any way that suits you. To do this, use `IfSharp.Kernel.App.AddDisplayPrinter` to add a function that accepts your type and returns a record that includes a `ContentType: string` (which is interpreted as a MIME Content-Type) and a `Data: string`, which is the content that will be interpreted by your browser as the specified Content-Type.

Often this will be "text/html" and some HTML. Here's an example:

#!fsharp

type Person = 
    { Name: string }

Formatter<Person>.Register(
    fun people writer ->
        writer.Write("<b><i>Ahoy " + people.Name + "!</i></b>")
    , mimeType = "text/html")

#!markdown

After this the Person type will display in the requiested way:

#!fsharp

let samplePerson = { Name = "Walter" }

samplePerson

#!markdown

# Using JavaScript to display dynamic output

You can also use existing browser display technologies directly in your Azure Notebook. Start by loading D3:

#!fsharp

@"<script src=""https://d3js.org/d3.v3.min.js""></script>" |> HtmlString

#!markdown

Now define a few F# types and functions that will let you write D3 output as structured F# data. These types are pretty simple, and don't cover everything D3 can do, but it shows how F# can interact with JavaScript directly in your notebook.

#!fsharp

type D3Op = {
    Op: string
    Style: List<string * string>
    Attr: List<string * int>
    On: List<string * List<D3Op>>
}

type D3 = List<D3Op>

let d3op = {Op = ""; Style = []; Attr = []; On = []}

let mapConcat s f xs = 
    xs |> List.map f |> String.concat s

let rec D3OpPrinter (op: D3Op) =
    sprintf 
        "%s%s%s%s"
        (if op.Op.Length > 0 then
            sprintf "\t.append(\"%s\")\n" op.Op
        else
            "")
        (op.Style |> mapConcat "\t" (fun (k, v) -> sprintf ".style(\"%s\", \"%s\")\n" k v ) )
        (op.Attr |> mapConcat "\t" (fun (k, v) -> sprintf ".attr(\"%s\", %d)\n" k v))
        (op.On
            |> mapConcat "\t" (fun (k, v) ->
                sprintf 
                    ".on(\"%s\",\n\tfunction(){\n\td3.select(this)\n%s\t})\n"
                    k
                    (v |> mapConcat "" D3OpPrinter)
                ))

let D3Printer (d3: D3) =
    sprintf 
        """
<div id="viz"></div>
<script type="text/javascript">

d3.select("#viz")
%s
"""
        (d3 |> mapConcat "" D3OpPrinter)

#!markdown

Now use D3 to render a circle that changes color when you put your mouse over it, by writing structured data:

#!fsharp

[
    {d3op with 
        Op = "svg"
        Attr = [("width", 100); ("height", 100)] }
    {d3op with
        Op = "circle"
        Style = [("stroke", "grey"); ("fill", "white")]
        Attr = [("r", 40); ("cx", 50); ("cy", 50)]
        On = 
          [ ("mouseover", [{d3op with Style = [("fill", "blue")]}])
            ("mouseout", [{d3op with Style = [("fill", "white")]}]) ] }
] |> D3Printer |> HtmlString

#!markdown

## Additional resources

The [F# Language Reference](https://docs.microsoft.com/dotnet/articles/fsharp/) is a reference for the F# language, and the [F# Guide](https://docs.microsoft.com/dotnet/articles/fsharp/) covers general topics.

[F# on Azure](https://docs.microsoft.com/dotnet/articles/fsharp/using-fsharp-on-azure/) is a technical guide for using F# in conjunction with a range of Azure services.
